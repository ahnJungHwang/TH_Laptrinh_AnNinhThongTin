@echo off
setlocal enabledelayedexpansion

:: Di chuyển vào thư mục hiện tại
cd /d %~dp0

:: Tự động bổ sung PATH cho openssl nếu có sẵn từ Git hoặc OpenSSL-Win64
where openssl >nul 2>nul
if %errorlevel% neq 0 (
    if exist "C:\Program Files\Git\usr\bin\openssl.exe" (
        set "PATH=C:\Program Files\Git\usr\bin;%PATH%"
    ) else if exist "C:\Program Files\OpenSSL-Win64\bin\openssl.exe" (
        set "PATH=C:\Program Files\OpenSSL-Win64\bin;%PATH%"
    )
)

:: Tạo các thư mục con trong certs/
mkdir certs\ca 2>nul
mkdir certs\server 2>nul
mkdir certs\client 2>nul

:: CA
openssl genrsa -out certs\ca\ca.key 2048
openssl req -x509 -new -nodes -key certs\ca\ca.key -sha256 -days 3650 -out certs\ca\ca.crt -config openssl.cnf -extensions v3_ca

:: Server
openssl genrsa -out certs\server\server.key 2048
openssl req -new -key certs\server\server.key -out certs\server\server.csr -subj "/C=VN/ST=HN/L=HN/O=MyOrg/OU=IT Dept/CN=localhost"
openssl x509 -req -in certs\server\server.csr -CA certs\ca\ca.crt -CAkey certs\ca\ca.key -CAcreateserial -out certs\server\server.crt -days 365 -sha256

:: Client
openssl genrsa -out certs\client\client.key 2048
openssl req -new -key certs\client\client.key -out certs\client\client.csr -subj "/C=VN/ST=HN/L=HN/O=MyOrg/OU=IT Dept/CN=client"
openssl x509 -req -in certs\client\client.csr -CA certs\ca\ca.crt -CAkey certs\ca\ca.key -CAcreateserial -out certs\client\client.crt -days 365 -sha256

:: Đổi tên file serial để tránh đè
move certs\ca\ca.srl certs\ca\ca.srl.bak >nul 2>&1

echo.
echo ===============================
echo Cac chung chi da tao xong!
echo - CA: certs\ca\
echo - Server: certs\server\
echo - Client: certs\client\
echo ===============================
pause
